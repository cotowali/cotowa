/// Copyright (c) 2021 zakuro <z@kuro.red>. All rights reserved.
///
/// This Source Code Form is subject to the terms of the Mozilla Public
/// License, v. 2.0. If a copy of the MPL was not distributed with this
/// file, You can obtain one at https://mozilla.org/MPL/2.0/.

// if powershell, skip most parts of std for now

fn print(s: string) {
  sh { printf '%%s' "$%s" }
  pwsh { [Console]::Write(%s) }
}

fn println(s: string) {
  sh { printf '%%s\n' "$%s" }
  pwsh { [Console]::WriteLine(%s) }
}

fn eprint(s: string) {
  sh { printf '%%s' "$%s" >&2 }
  pwsh { [Console]::Error.Write(%s) }
}

fn eprintln(s: string) {
  sh { printf '%%s\n' "$%s" >&2 }
  pwsh { [Console]::Error.WriteLine(%s) }
}

#if !pwsh
fn isatty(fd: int): bool {
  var true_ = true
  var res = false
  sh {
    if [ -t $%fd ]
    then
      %res=$%true_
    fi
  }
  return res
}

var tty = '/dev/tty'

fn input(prompt: string): string {
  if isatty(0) && prompt != '' {
    print(prompt) |> tty
  }
  var res: string
  sh {
    read -r %res
  }
  return res
}

fn input_tty(prompt: string): string {
  if prompt != '' {
    print(prompt) |> tty
  }
  var res: string
  sh {
    read -r %res < $%tty
  }
  return res
}
#endif

fn has_command(command: string): bool {
  var code: int
  sh {
    type $%command > /dev/null 2>&1
    %code=$?
  }
  pwsh {
    get-command $%command > /dev/null 2>&1
    if ($?) {
      %code = 0
    } else {
      %code = 1
    }
  }
  return code == 0
}

fn require_command(command: string) {
  if has_command(command) {
    return
  }

  eprintln("$command: command not found")
  exit(127)
}

fn ...string |> join(sep: string) |> string {
  var ret = ''
  var (i, s): (int, string)
  while read(&s) {
    if i > 0 {
      ret += sep
    }
    ret += s
    i += 1
  }
  return ret
}

fn range(begin: int, end: int) |> ...int {
  // cannot use negative number on busybox
  for i in seq(end - begin) {
    yield (i as int) + begin - 1
  }
}

// -- posix --

fn exit(code: int)

fn string |> cat(files: ...string) |> ...string

#if pwsh
fn seq(n: int) |> ...int {
  pwsh { 1..%n }
}
#else
fn seq(n: int) |> ...int
#endif

#if !pwsh
fn basename(path: string): string

fn cd(path: string)

fn dirname(path: string): string

fn mkdir(args: ...string)

fn mkdir_p(args: ...string) {
  sh {
    mkdir -p "$@"
  }
}

fn pwd(): string

fn ls(files: ...string) |> ...string

fn rm(paths: ...string)

fn rm_r(paths: ...string) {
  sh {
    rm -r "$@"
  }
}

fn touch(files: ...string)

#[mangle]
fn which(name: string): string {
  var res: string
  sh {
    %res=$(which $%name)
    if [ $? -ne 0 ]
    then
      %res=''
    fi
  }
  return res
}
#endif

// --- string methods ---

fn (s: string) replace(a: string, b: string): string {
#if pwsh

  var ret: string
  pwsh {
    %ret = %s -replace %a, %b
  }
  return ret

#else

  return s |> @sed("s/$a/$b/g")

#endif
}

fn (s: string) substr(i: int, n: int): string {
  if n <= 0 {
    return ''
  }
#if pwsh

  var ss: string
  pwsh {
    if (%i + %n -lt %s.length) {
      %ss = %s.Substring(%i, %n)
    } else {
      %ss = %s.Substring(%i)
    }
  }
  return ss

#else

  return (s |> @awk('-v', 'RS=""', '-v', "m=${i + 1}", '-v', "n=$n", '{print substr($0, m, n)}'))

#endif
}

fn (s: string) index(t: string): int {
#if pwsh

  var i: int
  pwsh { %i = %s.IndexOf(%t) }
  return i

#else

  if t.len() == 0 {
    return 0
  }
  return (s |> @awk('-v', "t=$t", '{print index($1, t) - 1}')) as int

#endif
}

fn (s: string) last_index(t: string): int {
#if pwsh

  var i: int
  pwsh { %i = %s.LastIndexOf(%t) }
  return i

#else

  var s_len = s.len()
  if t.len() == 0 {
    return s_len
  }

  // TODO: this implementation is inefficient

  var last_i = -1
  var rest = s
  while true {
    var i = rest.index(t)
    if i < 0 {
      break
    }
    last_i = i + (s_len - rest.len())
    rest = rest.substr(i + t.len(), rest.len())
  }
  return last_i

#endif
}

fn (s: string) len(): int {
  var n: int
  sh { %n=${#%s} }
  pwsh { %n=%s.length }
  return n
}

fn (s: string) starts_with(ss: string): bool {
  if ss.len() == 0 {
    return true
  }
  if ss.len() > s.len() {
    return false
  }
  if ss == s {
    return true
  }
  return s.index(ss) == 0
}

fn (s: string) ends_with(ss: string): bool {
  if ss.len() == 0 {
    return true
  }
  if ss.len() > s.len() {
    return false
  }
  if ss == s {
    return true
  }
  return s.last_index(ss) == s.len() - ss.len()
}

fn (s: string) trim_prefix(prefix: string): string {
  if !s.starts_with(prefix){
    return s
  }
  return s.substr(prefix.len(), s.len())
}

fn (s: string) trim_suffix(suffix: string): string {
  if !s.ends_with(suffix) {
    return s
  }
  return s.substr(0, s.len() - suffix.len())
}

fn (s: string) trim_start(): string {
#if pwsh

  var ret: string
  pwsh { %ret = %s.TrimStart() }
  return ret

#else

  return s |> @sed('s/^[[:space:]]*//')

#endif
}

fn (s: string) trim_end(): string {
#if pwsh

  var ret: string
  pwsh { %ret = %s.TrimEnd() }
  return ret

#else

  return s |> @sed('s/[[:space:]]*$//')

#endif
}

fn (s: string) trim(): string {
  return s.trim_start().trim_end()
}

fn (s: string) contains(substr: string): bool {
  return s.index(substr) >= 0
}

// --- array methods ---
// All array inherits []any

fn (arr: []any) len(): int {
  var n: int
  sh { %n=$(array_len %arr) }
  pwsh { %n = %arr.length }
  return n
}

fn (ss: []string) join(sep: string): string {
  if ss.len() == 0 {
    return ''
  }
  var ret: string
  for i in range(0, ss.len()) {
    if i > 0 {
      ret += sep
    }
    ret += ss[i]
  }
  return ret
}
