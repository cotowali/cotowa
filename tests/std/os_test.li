require 'platform'
require 'os'

fn test_env() {
  var key = '__custom__env__key__'
  assert(os::getenv(key) == '')
  os::setenv(key, 'v1')
  assert(os::getenv(key) == 'v1')
  os::setenv(key, 'v2')
  assert(os::getenv(key) == 'v2')
  os::unsetenv(key)
  assert(os::getenv(key) == '')
  os::unsetenv(key)
}
test_env()

fn test_path() {
  var res = os::path::join('a', 'b', 'c')
#if compiler_windows
  assert(res == r'a\b\c')
#else
  assert(res == 'a/b/c')
#endif
}
test_path()

fn test_clean_path() {
  if platform::is_busybox('') {
    return
  }

  var sep = os::path::separator
  // a/b/c -> a/b/c
  assert(os::path::clean(os::path::join('a', 'b', 'c')) == os::path::join('a', 'b', 'c'))
  // /a/../../ -> /
  assert(os::path::clean(os::path::join("${sep}a", '..', "..$sep")) == sep)
  // a/../.. -> ..
  assert(os::path::clean(os::path::join('a', '..', '..')) == '..')
  // ./././ -> .
  assert(os::path::clean(os::path::join('.', '.', '.')) == '.')
  // ../../.. -> ../../..
  assert(os::path::clean(os::path::join('..', '..', '..')) == os::path::join('..', '..', '..'))
  // /a/../b -> /b
  assert(os::path::clean(os::path::join("${sep}a", '..', 'b')) == "${sep}b")
  // /a/../b/../c -> /c
  assert(os::path::clean(os::path::join("${sep}a", '..', 'b', '..', 'c')) == "${sep}c")
  // /a/../b../ -> /b..
  assert(os::path::clean(os::path::join("${sep}a", '..', 'b..')) == "${sep}b..")
  // a/b/../c -> a/c
  assert(os::path::clean(os::path::join("a", 'b', '..', 'c')) == os::path::join('a', 'c'))
}
test_clean_path()

fn test_abs_path() {
  var sep = os::path::separator
  assert(os::path::is_abs(sep))
  assert(os::path::is_abs(sep + os::path::join('a', 'b')))
  assert(!os::path::is_abs(os::path::join('.', 'a', 'b')))
  assert(!os::path::is_abs(os::path::join('..', 'a', 'b')))
  assert(!os::path::is_abs(''))
  assert(!os::path::is_abs('.'))

  assert(os::path::abs('') == pwd())
  assert(os::path::abs('.') == pwd())
  assert(os::path::abs('a') == os::path::join(pwd(), 'a'))
  assert(os::path::abs(sep) == sep)
  if !platform::is_busybox('') {
    assert(os::path::abs('..') == os::path::clean(os::path::join(pwd(), '../')))
  }
}
test_abs_path()

fn test_path_exists() {
  var dirs = [
    '.',
    '..',
    pwd(),
    './././.',
    os::getenv('HOME'),
    '../../../../../../../../../../../../../../../',
    '/',
    '//',
    '../cotowali',
  ]
  for dir in dirs {
    assert(os::path::exists(dir), "exists('$dir')")
    assert(!os::path::is_file(dir), "!is_file('$dir')")
    assert(os::path::is_dir(dir), "is_dir('$dir')")
  }

  var files = [
    'README.md',
    './README.md',
    '../cotowali/README.md',
    'std/os.li',
    './std/os.li',
    '../cotowali/std/os.li',
  ]
  for file in files {
    assert(os::path::exists(file), "exists('$file')")
    assert(os::path::is_file(file), "is_file('$file')")
    assert(!os::path::is_dir(file), "!is_dir('$file')")
  }

  var not_exists = [
    'unknown_xxxxxx',
    './unknown_xxxxxx',
    '...',
    '~',
    '$HOME',
  ]
  for path in not_exists {
    assert(!os::path::exists(path), "!exists('$path')")
    assert(!os::path::is_file(path), "!is_file('$path')")
    assert(!os::path::is_dir(path), "!is_dir('$path')")
  }
}
test_path_exists()

fn test_symlink() {
  var prefix = '/tmp/symlink'

  var readme_link = prefix + 'README.md'
  @ln('-s', os::path::abs('README.md'), readme_link)
  assert(os::path::is_file(readme_link))
  assert(os::path::is_symlink(readme_link))
  rm(readme_link)

  var cotowali_link = prefix + 'cotowali'
  @ln('-s', os::path::abs('cotowali'), cotowali_link)
  assert(os::path::is_dir(cotowali_link))
  assert(os::path::is_symlink(cotowali_link))
  rm(cotowali_link)

  assert(os::path::exists('README.md'))
  assert(!os::path::is_symlink('README.md'))
  assert(os::path::exists('cotowali'))
  assert(!os::path::is_symlink('cotowali'))
}
test_symlink()
